\cancel mode verify

! plot_ambient.jnl
! Plot Ambient Weather data 
!  For indoor or outdoor variable: 
!    daily max, monthly overall max, monthly average of daily max
!    daily min, monthly overall min, monthly average of daily min
! Color the data by year
!
! Arguments are 
! 1) var_name 
! 2) transformation max or min
! 3) 1 to show Everett avgs on plots, or 2 to show Seattle avgs 
!

cancel data/all
cancel var/all
cancel symbol/all
cancel view

! Call plot_rain.jnl to plot daily and monthly rain totals.
! If no variable is entered, the script will list the variable names and stop.


! Open the monthly datasets, aggregating in time
go ambient_aggregate.jnl

IF `($1"0|0|*>1") EQ 0` THEN
   say Arguments are 
   say   1) var_name 
   say   2) transformation max or min 
   say   3) (optional) 1 to show Everett avgs on plots, or 2 to show Seattle avgs
   say
   SHOW DATA
   say
EXIT/SCRIPT

ENDIF

DEFINE SYMBOL var = `dncase("($1)")`


IF `($2"0|0|max>1|min>1|*>0") EQ 0` THEN
   say Argument 2 can be max or min, using max
   DEFINE SYMBOL trans = max
ELSE
   DEFINE SYMBOL trans = `dncase("($2)")`
ENDIF

DEFINE SYMBOL reference = 0
IF ($3"0|0|1|2>1|*>1") THEN 
   DEFINE SYMBOL reference = ($3)
   IF ($reference"0|1|*>0") THEN
      DEFINE SYMBOL reference_data = data/everett_climatology.nc
      DEFINE SYMBOL reference_title = Everett
   ELIF ($reference"0|2>1|*>0") THEN
      DEFINE SYMBOL reference_data = data/seattle_climatology.nc
      DEFINE SYMBOL reference_title = Seattle 
   ELSE
      say Argument 3 may be 1 to show Everett averages or 2 to show Seattle averages. Ignoring.
      DEFINE SYMBOL reference = 0
   ENDIF

   IF `($reference) GT 0 AND (($var"|outdoor_temperature>1|daily_rain>1|*>0") EQ 0)` THEN
      say
      say Everett or Seattle data plotted with Outdoor_Temperature or Daily_Rain only
      say Ignoring reference-data setting for ($var)
      DEFINE SYMBOL reference = 0
      say
      pause
   ENDIF
ENDIF

IF ($var"|daily_rain>1|*>0") THEN
   GO plot_rain ($reference)
   EXIT/SCRIPT
ENDIF


cancel mode logo
cancel view

! set mode ver:always
set text/font=arial


LET tt = t[gt=($var)]

LET nt = `tt,return=size`

LET firsty = `tax_datestring(tt[L=1], tt, "year")`
LET lasty_s = tax_datestring(tt[L=`nt`], tt, "year")
LET lasty = `lasty_s`

LET ny = lasty - firsty + 1

IF `ny gt 7` THEN
   say Script needs more color definitions for `ny` years
   EXIT/SCRIPT
ENDIF

! Define some viewports
repeat/range=2:`ny`/name=yy (\
DEFINE SYMBOL yy = `yy`;\
define view/x=0:1/y=0:1 full($yy);\
define view/x=0:1/y=0.5:1/text=1 upper($yy);\
define view/x=0:1/y=0:0.5/text=1 lower($yy);\
)

! Define colors to show year by year data

go write_ambient_palette

go draw_ambient_plots


