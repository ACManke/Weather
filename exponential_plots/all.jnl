!   

can mode logo
set mode verify
set text/font="Consolas"

cancel memory

define symbol fcntype = `UPCASE(SUBSTRING("($1%exp%)", 1,3))`

let type = ($fcntype"1|EXP>1|QUA>2|LIN>3")

IF `type eq 1` then 
   define symbol equation = val^2
   define symbol title = Exponential Growth, Doubling at Each Step
endif
IF `type eq 2` then 
   define symbol equation = val*val
   define symbol title = Quadratic Growth, The Value times itself at Each Step
endif
if `type eq 3` then 
   define symbol equation = 2*val
   define symbol title = Linear Growth, the Value times 2 at Each Step
endif

set win/aspect=1/size=2
go margins 0.7, 0.5, 0.1, 1.1
go margins 1, 1, 0.8, 1.2

! Draw a circle in a square window

let pi = atan(1.)*4
let deg2rad = pi/180
let theta = i[i=1:361]* deg2rad
let r = 1
let xx = r * cos(theta)
let yy = r * sin(theta)
plot/vs/line/thick=3/nolab/hlim=-1:1/vlim=-1:1/noax xx,yy
annotate/norm/xpos=0.5/ypos=1.04/halign=0/siz=0.2 "($title)"

let base = 2
let steps = 16

! This is the max for the exponential case; define the xpts,ypts,valiues
! to be variables this size, and we'll use as much as we need.
let big = base ^ steps

! Random points within a circle of radius R = 0.98
let r = 0.98
let rpts = r* (RANDU2(j[j=1:`big`],-1)) ^ 0.5
let theta = RANDU(j[j=1:`big`]) * 2 * pi

! Convert this to Cartesian coordinates

let xpts = rpts * COS(theta)
let ypts = rpts * SIN(theta)

LET values = MOD( j[j=1:`big`],10)	! value for colors at each (x,y) point

palette ten_by_levels

let vallast = 1
let val = 1
let xp = 1.12  ! for /halign=1
let xp = 1.0
let yp = 0.94
let ydel = 0.04
let asize = 0.12

IF `type eq 1` then

repeat/range=1:`steps`/name=q (\
define symbol q = `q`;\
let val = `2 * val`;\
plot/over/vs/ribbon/sym=circfill/siz=0.2/lev=(0,10,1)/j=`vallast`:`val`/nolab/noax xpts,ypts,values;\
ANNOTATE/NORM/xpos=`xp`/ypos=`yp`/halign=-1/siz=`asize` "`base`<sup>`q`</sup> = `val`";\
let vallast = `val+1`;\
let yp = `yp - ydel`;\
IF `q LT steps` then pause)

endif

if `type eq 2` then
let asize=.11

repeat/range=1:`steps`/name=q (\
define symbol q = `q`;\
let val = `($q) * ($q)`;\
plot/over/vs/ribbon/sym=circfill/siz=0.2/lev=(0,10,1)/j=`vallast`:`val`/nolab/noax xpts,ypts,values;\
annotate/norm/xpos=0.5/ypos=1.04/halign=0/siz=0.2 "($title)";\
ANNOTATE/NORM/xpos=`xp`/ypos=`yp`/halign=-1/siz=`asize` "($q) x ($q) = `val`";\
let vallast = `val+1`;\
let yp = `yp - ydel`;\
IF `q LT steps` then pause)

endif

if `type eq 3` then
let asize=.11

repeat/range=1:`steps`/name=q (\
define symbol q = `q`;\
let val = `2* ($q)`;\
plot/over/vs/ribbon/sym=circfill/siz=0.2/lev=(0,10,1)/j=`vallast`:`val`/nolab/noax xpts,ypts,values;\
ANNOTATE/NORM/xpos=`xp`/ypos=`yp`/halign=-1/siz=`asize` "($q) x 2 = `val`";\
let vallast = `val+1`;\
let yp = `yp - ydel`;\
IF `q LT steps` then pause)

endif
