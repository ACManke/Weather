! rainbow temperature scarf of one year
!
can mode logo
set mode ver

cancel data/all
cancel var/all
cancel symbol/all
cancel view
cancel region

set mode calendar:day

set text/font=arial
define view/axes/x=.02:1/y=0:1 nomargin

let yr = $1%2021%
define symbol start_day = $2%20-sep%
define symbol palette = $3%rnb2%


cancel view

go ambient_aggregate.jnl

let deld = 5/(24*60)  ! delta 5 minutes, units of a day
define axis/t="1-jan-2020:00:00":"31-dec-2023:23:55":`deld`/units=days/t0="1-jan-2020:00:00"/edges time5
set var/name=temp_in temperature
let/like=temp_in temperature = temp_in[gt=time5]


let tt = t[gt=temperature]
let nt = `tt,return=size`

let firsty = `tax_datestring(tt[L=1], tt, "year")`
let lasty_s = tax_datestring(tt[L=`nt`], tt, "year")
let lasty = `lasty_s`


let ny = lasty - firsty + 1
define axis/t=1-jan-`firsty`:31-dec-`lasty`:1/edges/units=days tday


let/like=temperature temp_max = temperature[gt=tday@max]
let/like=temperature temp_min = temperature[gt=tday@min]

set region/t=1-jan-`yr`:31-dec-`yr`
set region/t=($start_day)-`yr-1`:($start_day)-`yr`
if ($start_day%|1-jan>1|01-jan>1|*>0%) then \
   set region/t=($start_day)-`yr`:31-dec-`yr`


! set region/t=1-nov-`yr-1`:1-nov-`yr`

let/like=temperature/title="Daily Max Temp" day_temp_max = temp_max[t=@fav]
let/like=temperature/title="Daily Min Temp" day_temp_min = temp_min[t=@fav]

let tt = t[gt=tday]

define ax/y=1:2:1/units=hours yax
let yy = y[gy=yax]

let tempmax2d = day_temp_max + 0*yy
let tempmin2d = day_temp_min + 0*yy

let temp2d_a = if yy eq 1 then tempmin2d else 0
let temp2d_b = if yy eq 2 then tempmax2d else 0

let day2d = temp2d_a + temp2d_b
define symbol levs = (-inf)(30,90,5)(inf)

! set window/siz=2
! set view upper
! go margins .8 .8
! 
! 
! shade/pal=($palette)/title="Low and High Daily Temperature"/lev=($levs)/set day2d
!  ppl ylab "`temperature,return=units`"
! ppl shade

! set view lower
! go margins .8 .8


define axis/y=0:24/npoints=144/units=hours yax
let yy = y[gy=yax]
let yy2d = yy + 0*ll

go hours_per_day 0 0
let ll  = (tt - `tt[L=1]`)  ! axis of the temperature data

let yy2d = yy + 0*ll
let daylen2d = 24 - daylen+ 0*yy

let mask1 = if yy2d lt daylen2d then 1


let aspect = (24*6/7)/(365*2/10)	! 365*2 days long (2 rows/day), 24 hours by 10 minutes wide
					! row gauge 10 row/inch, stitch gauge 7 stitch/inch


set window/siz=2/asp=`aspect` 
set view nomargin
go margins .3 .3

shade/pal=($palette)/lev=($levs)/vlim=2:22/title="Low and High Daily Temperature"/set mask1*tempmin2d
  go unlabel ($labnum_dset)
  ppl ylab "hours"
 ppl shakey 1, 1, 0.08, , , , 0.5, 0.7, 0.8, 3.2
ppl shade

let mask2 = if yy2d ge daylen2d then 1
shade/over/nolab/pal=($palette)/lev=($levs) mask2*tempmax2d

annotate/norm/x=0.5/y=1.04/siz=0.1/halign=0 "Low and High Daily Temperature (Deg F) ($start_day)-`yr-1`:($start_day)-`yr`"
frame/file=scarf_2022_2023.png

! 144 stitches, 730 rows
! 
! < 30	Deep purple		Eggplant
! 30-34	purple			French Lavendar
! 35-39	blue-purple		Majestic
! 40-44	blue			Jay
! 45-49	med blue		Blue
! 50-54	cyan			Cyan
! 55-59	green			Spruce
! 60-65	apple Ggreen		Macaw
! 65-69	chartreuse		Limeade
! 70-74	yellow			Canary
! 75-79	lt orange		Kumquat
! 80-84	red-orange		Orange
! 85-89	red			Serrano
! > 90	deep red		Garnet

! Stitches across the row are one per 10 minutes for a total of 144
let/title="# Night"/units="stitches" night_sts = INT((24-daylen)*6 + 0.5)
let/title="# Day"/units="stitches" day_sts = INT(daylen*6 + 0.5)

let/like=day_temp_min day_min = INT(day_temp_min + 0.5)
let/like=day_temp_max day_max= INT(day_temp_max + 0.5)

list/prec=3/t=20-sep-`yr-1`:1-jan-`yr` night_sts, day_sts, day_min, day_max


