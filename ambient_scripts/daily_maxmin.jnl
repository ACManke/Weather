! Define daily max and min variable values

cancel mode logo
set text/font=helvetica

go ambient_aggregate.jnl

! Arguments
!   $1  variable (default is temperature)
!   $2  start date - by default use entire range
!   $3  end date 


define symbol var = ($1"temperature|*")


let tt = t[gt=($var)]

let nt = `tt,return=size`

let firsty = `tax_datestring(tt[L=1], tt, "year")`
let lasty_s = tax_datestring(tt[L=`nt`], tt, "year")
let lasty = `lasty_s`

! Use the date range if given, or the entire data range
IF ($2"0|*>1") THEN 
   define symbol date_range = t=($2)
ELSE
   define symbol date_range = t=1-jan-`firsty`
ENDIF

IF `($3"0|*>1") EQ 0` THEN 
   define symbol date_range =  ($date_range):31-dec-`lasty`
ELSE
   define symbol date_range = ($date_range):($3)   
ENDIF

define symbol date_plot = /($date_range)

let ny = lasty - firsty + 1

define axis/t=1-jan-`firsty`:31-dec-`lasty`:1/edges/units=days tday

let/like=($var)/title="Daily Max ($var)" day_max = ($var)[gt=tday@max]

let/like=($var)/title="Daily Min ($var)" day_min = ($var)[gt=tday@min]

show var day*


set win/asp=0.5/siz=1.5
plot/thick=1/color=(90,90,90)($date_plot)/title=" " ($var)
plot/over/thick=1.5/color=(10,0,75)/nolab($date_plot) day_min
plot/over/thick=1.5/color=(75,0,0)/nolab($date_plot) day_max

annotate/norm/x=0.5/y=1.1/halign=0 "`($var),return=title`"

annotate/norm/x=0/y=-0.23/siz=0.1 "Lowest Min `day_min[($date_range)@min],p=4`  Highest Min  `day_min[($date_range)@max],p=4`"
annotate/norm/x=0/y=-0.3/siz=0.1  "Lowest Max `day_max[($date_range)@min],p=4`  Highest Max  `day_max[($date_range)@max],p=4`  "

Say 
Say 
Say 
Say Daily ($var)
say Lowest Min `day_min[($date_range)@min],p=4`  Highest Min  `day_min[($date_range)@max],p=4`  
say Lowest Max `day_max[($date_range)@min],p=4`  Highest Max  `day_max[($date_range)@max],p=4`  

Say 
Say 
Say 
