! The variable and transformation are defined, as symbols 
! ($var) and ($trans). Draw the plots


cancel view
set win/aspect=0.5/size=2

define symbol trans_title = ($trans"|max>High|min>Low")

! Daily axis. Start 2 months before the start of the first year, to 
! allow for the plot of each years data overlaid, in the month-range of 
! the most recent 3 months of data.

define axis/t=1-nov-`firsty-1`:1-jan-`lasty+1`:1/edges/units=days tday

let/like=($var)/title="`($var),return=title` ($trans)" day_var_($trans) = ($var)[gt=tday@($trans)]

define symbol var_title = Daily ($trans_title) `($var),return=title`

!  Plot style for overlaying monthly reference data
if `($reference)` then\   
   define symbol graystyle = /nolab/color=(50,50,50)/dash=(0.05,0.05,0.05,0.05)/step=connected


! ----------------------
! Plotting a subset of the year - a few months before the current date
! To set particular dates, set date1 and date2 below call to date_range below
! date1 must be before date 2, a range within a single calendar year

DEFINE SYMBOL smoothing = 1
DEFINE SYMBOL yrm = 1

DEFINE SYMBOL date1 = 01-Jan
DEFINE SYMBOL date2 = 31-Dec
GO date_range

! variable to plot and plot-style.
let/like=day_var_($trans) var = day_var_($trans)
define symbol plotstyle=/thick=2/step=connected

let/like=day_var_($trans) var = day_var_($trans)[L=@SBX:($smoothing)]
define symbol plotstyle=/thick=2

! Get the axis limits for this subset and the vertical range for plotting
! the years on a common vertical axis

go color_labels ($date1) ($date2)

define symbol plot_title = ($var_title)<NL> ($color_label)
IF `($smoothing) GT 1` THEN define symbol plot_title = ($var_title), ($smoothing)-day smoothed  <NL> ($color_label)

let dax_lo =  `INT(dax_lo - 1)`
let dax_hi =  `INT(dax_hi + 1)`

define symbol plotstyle=($plotstyle)/vlim=`dax_lo`:`dax_hi`

set view full


define symbol timerange = /t=($date1)-`firsty`:($date2)-`firsty`
IF ($yrm"0") THEN define symbol timerange = /t=($date1)-`firsty-1`:($date2)-`firsty`
plot($plotstyle)/color=($color1)($timerange)/title="($plot_title)"/set var
go unlabel ($lab($labnum_year))
ppl plot


let yr = firsty+1

repeat/range=2:`ny`/name=yy (\
 define symbol yy = `yy`;\
 set view full($yy);\
 define symbol timerange = /t=($date1)-`yr`:($date2)-`yr`;\
 IF ($yrm"0") THEN define symbol timerange = /t=($date1)-`yr-1`:($date2)-`yr`;\
 plot($plotstyle)/color=($color($yy))/axes=0,0,0,0/nolab($timerange) var;\
 let yr = `yr + 1`)


annotate/norm/x=0.5/y=1.05/siz=0.14/halign=0 "Ambient Weather `($var),return=title` Data, recent months"


say Plotting smoothed data for ($date1) to ($date2).  Edit draw_ambient_plots.jnl to change the date range or add smoothing
pause

cancel symbol yrm

! ---------------------- daily for entire year

go color_labels 01-Jan 31-Dec

! What's the overall min/max?  Use for the v-limits 

let ax_lo =  `INT(day_var_($trans)[t=@min]) - 1`
let ax_hi =  `INT(day_var_($trans)[t=@max]) + 1`

IF `STRINDEX("($var)", "rain") GT 0` THEN LET ax_lo = 0


define symbol plot_title = ($var_title)  <NL> ($color_label)

set view full
plot/color=($color1)/thick/vlim=`ax_lo`:`ax_hi`/title="($plot_title)" day_var_($trans)[gt=tday, t=1-jan-`firsty`:31-dec-`firsty`]

let yr = firsty + 1
cancel symbol yearlab

repeat/range=2:`ny`/name=yy (\
 define symbol yy = `yy`;\
 set view full($yy);\
 plot/color=($color($yy))/thick/axes=0,0,0,0/nolab/vlim=`ax_lo`:`ax_hi` day_var_($trans)[t=1-jan-`yr`:31-dec-`yr`];\
 let yr = `yr + 1`)

IF `ny GT 1` THEN define symbol yearlab = ($yearlab) - `yr-1`
annotate/norm/x=0.523/y=-0.132/siz=0.11/halign=-1 " ($yearlab)";\

annotate/norm/x=0.5/y=1.05/siz=0.14/halign=0 "Ambient Weather `($var),return=title` Data"

pause
 

! ---------------------------------------------------------------------
!  Monthly data
! Define a monthly axis starting in Jan `firsty`

LET start_year = `firsty`

LET indices = L[L=1:`ny*12 + 1`]
LET month = MOD(indices-1,12)+1
LET year = start_year + INT((indices-1)/12)
DEFINE AXIS/UNITS=days/T0=1-jan-1900/EDGES tmonth = DAYS1900(year,month,1)


go color_labels 01-Jan 31-Dec

! ---------------------------------------------------------------------
!  Monthly Maximum or Minimum high 
cancel view
set window/size=2/aspect=1


define symbol plot_annotation = ($trans_title)est Daily `($var),return=title` in month (Deg F)

IF ($trans"max>1|*>0") THEN define symbol plot_annotation = Monthly Maximum High Temp (Deg F)
IF ($trans"min>1|*>0") THEN define symbol plot_annotation = Monthly Minimum Low Temp (Deg F)


let/units="`day_var_($trans),return=units`" monthly =  day_var_($trans)[gt=tmonth@($trans)]
let/units="`day_var_($trans),return=units`" month_temp =  if monthly then monthly else 0

let mm = L[gt=month_temp]
let color = INT( (mm-1)/12) + 1

let ax_add = 4
define symbol units = `($var),return=units`
IF `strindex(upcase("($units)"), "INCH") GT 0` THEN let ax_add = 1

let ax_lo = INT(monthly[t=@min]) - ax_add
let ax_hi = INT(monthly[t=@max]) + ax_add

IF `STRINDEX("($var)", "rain") GT 0` THEN LET ax_lo = 0

set view upper
plot/step=conn/nolab/color=black/vlim=`ax_lo`:`ax_hi`/title=" " month_temp

! Draw a bar-chart showing monthly data

let lev_hi = ny + 0.5
go bar_chart3 poly/axes/over/nolab/fill/line/pal=ambient_palette/lev=(0.5,`lev_hi`,1) month_temp color
annotate/norm/x=0.5/y=1.5/halign=0/siz=0.2 "($plot_annotation)"

set view lower

define symbol plot_title = ($color_label)
define symbol plot_style = /nolab/step=connected/vlim=`ax_lo`:`ax_hi`/line/thick/nokey

plot/color=($color1)/($plot_style)/title="($plot_title)"/t=15-jan-`firsty`:15-dec-`firsty` monthly
plot/nolab/nokey/over/thick/color=($color1)/sym=26/t=15-jan-`firsty`:15-dec-`firsty` monthly


let yr = firsty + 1
cancel symbol yearlab

repeat/range=2:`ny`/name=yy (\
 define symbol yy = `yy`;\
 set view lower($yy);\
 plot/nolab/axes=0,0,0,0($plot_style)/color=($color($yy))/lev=(0.5,`lev_hi`,1)/t=15-jan-`yr`:15-dec-`yr` monthly;\
 plot/nolab/nokey/over/thick/color=($color($yy))/sym=26/t=15-jan-`yr`:15-dec-`yr` monthly;\
 let yr = `yr + 1`)

IF `ny GT 1` THEN define symbol yearlab = ($yearlab) - `yr-1`
annotate/norm/x=0.523/y=-0.27/siz=0.11/halign=-1 " ($yearlab)"
annotate/norm/x=0.523/y=-0.5/siz=0.18/halign=0 "($plot_title)"

annotate/norm/x=0.5/y=1.5/halign=0/siz=0.2 "($plot_annotation)"

! ---------------------------------------------------------------------
message now average monthly
set window/new/title="Average Temp"/size=2/aspect=1


!  Monthly Average high 

define symbol plot_annotation = Monthly Average of Daily ($trans_title) `($var),return=title` (Deg F)

let/units="`day_var_($trans),return=units`"  monthly =  day_var_($trans)[gt=tmonth@ave]
let/units="`day_var_($trans),return=units`" month_temp =  if monthly then monthly else 0

set view upper
plot/step=conn/nolab/color=black/vlim=`ax_lo`:`ax_hi`/title=" " month_temp

! Draw a bar-chart showing monthly
go bar_chart3 poly/axes/over/nolab/fill/line/pal=ambient_palette/lev=(0.5,`lev_hi`,1) month_temp color

annotate/norm/x=0.5/y=1.2/halign=0/siz=0.2 "($plot_annotation)"

if `($reference)` then
   use ($reference_data)
   if ($trans"0|max>1|*>0") then 
      plot/over($graystyle) temp_hi[gt=tmonth]
   elif ($trans"0|min>1|*>0") THEN
      plot/over($graystyle) temp_lo[gt=tmonth]
   endif
   annotate/norm/x=0/y=1.4/siz=0.12 "Gray: ($reference_title) Average Monthly Rainfall"

   cancel data ($reference_data)
   set data 1
endif


set view lower
define symbol plot_title = ($color_label)

define symbol plot_style = /nolab/step=connected/vlim=`ax_lo`:`ax_hi`/line/thick/nokey

plot/ribbon($plot_style)/pal=ambient_palette/lev=(0.5,`lev_hi`,1)/title="($plot_title)"/t=15-jan-`firsty`:15-dec-`firsty`   monthly, color
plot/nolab/over/thick/color=($color1)/sym=26/t=15-jan-`firsty`:15-dec-`firsty` monthly

let yr = firsty + 1
cancel symbol yearlab

repeat/range=2:`ny`/name=yy (\
 define symbol yy = `yy`;\
 set view lower($yy);\
 plot/nolab/axes=0,0,0,0/ribbon($plot_style)/nokey/pal=ambient_palette/lev=(0.5,`lev_hi`,1)/t=15-jan-`yr`:15-dec-`yr` monthly, color;\
 plot/nolab/nokey/over/thick/color=($color($yy))/sym=26/t=15-jan-`yr`:15-dec-`yr` monthly;\
 let yr = `yr + 1`)

IF `ny GT 1` THEN define symbol yearlab = ($yearlab) - `yr-1`
annotate/norm/x=0.523/y=-0.27/siz=0.11/halign=-1 " ($yearlab)"
annotate/norm/x=0.523/y=-0.5/siz=0.18/halign=0 "($plot_title)"

say " "
say "($plot_annotation)"
say " "

if `($reference)` then

   use ($reference_data)
   if ($trans"0|max>1|*>0") then 
      plot/over($graystyle) temp_hi[gt=tmonth]
   else
      plot/over($graystyle) temp_lo[gt=tmonth]
   endif
   cancel data ($reference_data)

elif ($var"|outdoor_temperature>1|OUTDOOR_TEMPERATURE>1|*>0") then

   say To see Everett or Seattle average monthly min temp with outdoor_temp plots,
   say add 3rd argument 1 for Everett or 2 for Seattle

endif

set data 1

say " "

! -------------- List monthly data --------------

let tm = t[gt=monthly]
let modate = TAX_DATESTRING(tm, tm, "month")

IF ($reference) THEN

   if ($trans"0|max>1|*>0") THEN 
      define symbol hilo_var = temp_hi
      define symbol hilo_lab = High
   else
      define symbol hilo_var = temp_lo
      define symbol hilo_lab = Low
   endif

   use data/everett_climatology.nc
   use data/seattle_climatology.nc

   set data 1

   let m1 = 1
   let yr = firsty
   cancel symbol comma
   cancel symbol varlist

   repeat/range=1:`ny`/name=yy (\
   let/title="Ambient `yr`" c`yr` = reshape(monthly[L=`m1`:`m1+11`], t[gt=month_irreg]);\
   define symbol varlist = ($varlist)($comma)c`yr`;\
   define symbol comma = , ;\
   let m1 = `m1+12`;\
   let yr = `yr+1`)

   let/like=($hilo_var)[d=2] Everett = ($hilo_var)[d=2]
   let/like=($hilo_var)[d=3] Seattle = ($hilo_var)[d=3]
   list ($varlist), Everett, Seattle

else

   let m1 = 1
   let yr = firsty
   cancel symbol comma
   cancel symbol varlist

   repeat/range=1:`ny`/name=yy (\
   let/title=c`yr` c`yr` = reshape(monthly[L=`m1`:`m1+11`], t[gt=month_irreg]);\
   define symbol varlist = ($varlist)($comma)c`yr`;\
   define symbol comma = ,;\
   let m1 = `m1+12`;\
   let yr = `yr+1`)
   list ($varlist)

endif
