! The variable and transformation are defined, as symbols 
! ($var) and ($trans). Draw the plots

define symbol trans_title = ($trans"|max>High|min>Low")

! Daily axis. Start 2 months before the start of the first year, to 
! allow for the plot of each years data overlaid, in the month-range of 
! the most recent 3 months of data.

define axis/t=1-nov-`firsty-1`:1-jan-`lasty+1`:1/edges/units=days tday

let/like=($var)/title="`($var),return=title` ($trans)" ($var)_day_($trans) = ($var)[gt=tday@($trans)]


define symbol var_title = Daily ($trans_title) `($var),return=title`

IF ($temp_diff"0|*>1") THEN
   
   let day_min = ($var)[gt=tday@min]
   let day_max = ($var)[gt=tday@max]
   let/like=($var)/title="`($var),return=title` day max-min" ($var)_day_diff = day_max - day_min
   define symbol var_title = Daily max-min `($var),return=title`
   define symbol trans = diff
   define symbol trans_title = High-Low

ENDIF


!  Plot style for overlaying monthly reference data
if `($reference)` then 
   define symbol graydash = /nolab/color=(30,30,30)/dash=(0.3,0.1,0.3,0.01)/step=connected
   define symbol graydash = /nolab/color=(30,30,30)/step=connected
   define symbol whitedash = /nolab/color=(99,99,99)/dash=(0.04,0.04,0.04,0.04)/step=connected
endif

! ----------------------
! Plotting a subset of the year - a few months before the current date
! To set particular dates, set date1 and date2 after the call to range_of_dates below
! date1 must be before date 2, a range within a single calendar year

define symbol smoothing = 5
if ($trans"0|diff>1|*>0") then define symbol smoothing = 5
define symbol yrm = 1

define symbol date1 = 01-Jan
define symbol date2 = 31-Dec

let nmon = 2  ! start nmon months before the current month (end of record)
GO range_of_dates `nmon`

! variable to plot and plot-style.
let/like=($var)_day_($trans) var = ($var)_day_($trans)
define symbol plotstyle=/thick=2/step=connected

let/like=($var)_day_($trans) var = ($var)_day_($trans)[L=@SBX:($smoothing)]
define symbol plotstyle=/thick=2

! Get the axis limits for this subset and the vertical range for plotting
! the years on a common vertical axis

go color_labels ($date1) ($date2)

define symbol plot_title = ($var_title)<NL> ($color_label)
IF `($smoothing) GT 1` THEN define symbol plot_title = ($var_title), ($smoothing)-day smoothed  <NL> ($color_label)

let dax_lo =  `INT(dax_lo - 1)`
let dax_hi =  `INT(dax_hi + 1)`

define symbol plotstyle=($plotstyle)/vlim=`dax_lo`:`dax_hi`

set window/aspect=0.5/size=2 1
set view full

define symbol timerange = /t=($date1)-`firsty`:($date2)-`firsty`
IF ($yrm"0") THEN define symbol timerange = /t=($date1)-`firsty-1`:($date2)-`firsty`
plot($plotstyle)/color=($color1)($timerange)/title="($plot_title)"/set var
go unlabel ($lab($labnum_year))
ppl plot


let yr = firsty+1

repeat/range=2:`ny`/name=yy (\
 define symbol yy = `yy`;\
 set view full($yy);\
 define symbol timerange = /t=($date1)-`yr`:($date2)-`yr`;\
 IF ($yrm"0") THEN define symbol timerange = /t=($date1)-`yr-1`:($date2)-`yr`;\
 plot($plotstyle)/color=($color($yy))/axes=0,0,0,0/nolab($timerange) var;\
 let yr = `yr + 1`)

IF `nmon GT 0` THEN
   annotate/norm/x=0.5/y=1.05/siz=0.14/halign=0 "Ambient Weather `($var),return=title` Data, recent months"
ELSE
   annotate/norm/x=0.5/y=1.05/siz=0.14/halign=0 "Ambient Weather `($var),return=title` Data, recent month"
ENDIF

say Plotting smoothed data for ($date1) to ($date2).  Edit draw_ambient_plots.jnl to change the date range or add smoothing
pause

cancel symbol yrm

! ---------------------- daily for entire year

go color_labels 01-Jan 31-Dec

! What's the overall min/max?  Use for the v-limits 

let ax_lo =  `INT(($var)_day_($trans)[t=@min]) - 1`
let ax_hi =  `INT(($var)_day_($trans)[t=@max]) + 1`

IF `STRINDEX("($var)", "rain") GT 0` THEN LET ax_lo = 0


define symbol plot_title = ($var_title)  <NL> ($color_label)

set view full
plot/color=($color1)/thick/vlim=`ax_lo`:`ax_hi`/title="($plot_title)  Average (black)" ($var)_day_($trans)[gt=tday, t=1-jan-`firsty`:31-dec-`firsty`]

let yr = firsty + 1
cancel symbol yearlab

repeat/range=2:`ny`/name=yy (\
 define symbol yy = `yy`;\
 set view full($yy);\
 plot/color=($color($yy))/thick/axes=0,0,0,0/nolab/vlim=`ax_lo`:`ax_hi` ($var)_day_($trans)[t=1-jan-`yr`:31-dec-`yr`];\
 let yr = `yr + 1`)

IF `ny GT 1` THEN define symbol yearlab = ($yearlab) - `yr-1`
annotate/norm/x=0.523/y=-0.133/siz=0.11/halign=-1 " ($yearlab)"


annotate/norm/x=0.5/y=1.05/siz=0.14/halign=0 "Ambient Weather `($var),return=title` Data"

! Average daily data
define view/x=0:1/y=0:1 fullmod
set view fullmod

define axis/t=0:365.2425/edges/npoints=365/t0=1-jan-0001/units=days/modulo tdaily
let/like=($var)_day_($trans) dailyclim=($var)_day_($trans)[gt=tdaily@mod]

plot/color=black/thick/axes=0,0,0,0/nolab/vlim=`ax_lo`:`ax_hi` dailyclim
pause

 

! ---------------------------------------------------------------------
!  Monthly data
! Define a monthly axis starting in Jan `firsty`

LET start_year = `firsty`

LET indices = L[L=1:`ny*12 + 1`]
LET month = MOD(indices-1,12)+1
LET year = start_year + INT((indices-1)/12)
DEFINE AXIS/UNITS=days/T0=1-jan-1900/EDGES tmonth = DAYS1900(year,month,1)


go color_labels 01-Jan 31-Dec

! ---------------------------------------------------------------------
!  Monthly Maximum or Minimum 

define symbol units = `($var),return=units`
let ax_add = 4
IF `strindex(upcase("($units)"), "INCH") GT 0` THEN let ax_add = 1
   
let lev_hi = ny + 0.5

! If its a max-min diff plot show the maximum difference

set window/new/size=2/aspect=1

IF ($trans"0|diff>1|*>0") THEN
   define symbol plot_annotation = Monthly Greatest outdoor temperature difference (Deg F)
   let/units="($units)" ($var)_month_($trans)  =  temperature_day_diff[gt=tmonth@max]
   let/units="($units)" month_var =  if ($var)_month_($trans) then ($var)_month_($trans) else 0

ELSE

   define symbol plot_annotation = Monthly ($trans_title)est `($var),return=title` (($units))
   
   let/units="($units)" ($var)_month_($trans) =  ($var)_day_($trans)[gt=tmonth@($trans)]
   let/units="($units)" month_var =  if ($var)_month_($trans) then ($var)_month_($trans) else 0

ENDIF

let mm = L[gt=month_var]
let color = INT( (mm-1)/12) + 1

let ax_lo = INT(($var)_month_($trans)[t=@min]) - ax_add
let ax_hi = INT(($var)_month_($trans)[t=@max]) + ax_add

IF `STRINDEX("($var)", "rain") GT 0` THEN LET ax_lo = 0

set view upper; go margins 1.4 0.8
plot/step=conn/nolab/color=black/vlim=`ax_lo`:`ax_hi`/title=" " month_var

! Draw a bar-chart showing monthly data

go bar_chart3 poly/axes/over/nolab/fill/line/pal=ambient_palette/lev=(0.5,`lev_hi`,1) month_var color
annotate/norm/x=0.5/y=1.2/halign=0/siz=0.2 "($plot_annotation)"

set view lower; go margins 0.8 1.4

define symbol plot_title = ($color_label)
define symbol plot_style = /nolab/step=connected/vlim=`ax_lo`:`ax_hi`/line/thick/nokey

plot/color=($color1)/($plot_style)/title="($plot_title)"/t=15-jan-`firsty`:15-dec-`firsty` ($var)_month_($trans)
plot/nolab/nokey/over/thick/color=($color1)/sym=26/t=15-jan-`firsty`:15-dec-`firsty` ($var)_month_($trans)


let yr = firsty + 1
cancel symbol yearlab

repeat/range=2:`ny`/name=yy (\
 define symbol yy = `yy`;\
 set view lower($yy); go margins 0.8 1.4;\
 plot/nolab/axes=0,0,0,0($plot_style)/color=($color($yy))/lev=(0.5,`lev_hi`,1)/t=15-jan-`yr`:15-dec-`yr` ($var)_month_($trans);\
 plot/nolab/nokey/over/thick/color=($color($yy))/sym=26/t=15-jan-`yr`:15-dec-`yr` ($var)_month_($trans);\
 let yr = `yr + 1`)

IF `ny GT 1` THEN define symbol yearlab = ($yearlab) - `yr-1`
annotate/norm/x=0.533/y=-0.205/siz=0.11/halign=-1 " ($yearlab)"

annotate/norm/x=0.5/y=-0.5/siz=0.14/halign=0 "($plot_title)"
   
say " "

! -------------- List monthly data --------------



   let m1 = 1
   let yr = firsty
   cancel symbol comma
   cancel symbol varlist

   repeat/range=1:`ny`/name=yy (\
   let/title="`yr` monthly ($trans_title)est" y`yr` = reshape(($var)_month_($trans)[L=`m1`:`m1+11`], t[gt=month_irreg]);\
   define symbol varlist = ($varlist)($comma)y`yr`;\
   define symbol comma = ,;\
   let m1 = `m1+12`;\
   let yr = `yr+1`)
   list ($varlist)



! ---------------------------------------------------------------------
message now average monthly
set window/new/size=2/aspect=1/title="Average ($var)"


!  Monthly Average high/low

define symbol plot_annotation = Monthly Average ($trans_title) `($var),return=title` (($units))

let/units="($units)"  ($var)_month_ave_($trans) =  ($var)_day_($trans)[gt=tmonth@ave]
let/units="($units)" month_var =  if ($var)_month_ave_($trans) then ($var)_month_ave_($trans) else 0

let mm = L[gt=month_var]
let color = INT( (mm-1)/12) + 1

let ax_lo = INT(($var)_month_ave_($trans)[t=@min]) - ax_add
let ax_hi = INT(($var)_month_ave_($trans)[t=@max]) + ax_add

set view upper; go margins 1.4 0.8
plot/step=conn/nolab/color=black/vlim=`ax_lo`:`ax_hi`/title=" " month_var

! Draw a bar-chart showing monthly
go bar_chart3 poly/axes/over/nolab/fill/line/pal=ambient_palette/lev=(0.5,`lev_hi`,1) month_var color

annotate/norm/x=0.5/y=1.2/halign=0/siz=0.2 "($plot_annotation)"

if `($reference)` then
   use ($reference_data)
   if ($trans"0|max>1|*>0") then 
      plot/over($graydash) temp_hi[gt=tmonth]
      plot/over($whitedash) temp_hi[gt=tmonth]
   elif ($trans"0|min>1|*>0") then
      plot/over($graydash) temp_lo[gt=tmonth]
      plot/over($whitedash) temp_lo[gt=tmonth]
   elif ($trans"0|diff>1|*>0") then
      plot/over($graydash) temp_hi[gt=tmonth] - temp_lo[gt=tmonth]
      plot/over($whitedash) temp_hi[gt=tmonth] - temp_lo[gt=tmonth]
   endif

   annotate/norm/x=0/y=1.4/siz=0.12 "Dash: ($reference_title) Monthly Average"

   cancel data ($reference_data)
   set data 1
endif


set view lower; go margins 0.8 1.4
define symbol plot_title = ($color_label)

define symbol plot_style = /nolab/step=connected/vlim=`ax_lo`:`ax_hi`/line/thick/nokey

plot/ribbon($plot_style)/pal=ambient_palette/lev=(0.5,`lev_hi`,1)/title="($plot_title)"/t=15-jan-`firsty`:15-dec-`firsty`   ($var)_month_ave_($trans), color
plot/nolab/over/thick/color=($color1)/sym=26/t=15-jan-`firsty`:15-dec-`firsty` ($var)_month_ave_($trans)

let yr = firsty + 1
cancel symbol yearlab

repeat/range=2:`ny`/name=yy (\
 define symbol yy = `yy`;\
 set view lower($yy); go margins 0.8 1.4;\
 plot/nolab/axes=0,0,0,0/ribbon($plot_style)/nokey/pal=ambient_palette/lev=(0.5,`lev_hi`,1)/t=15-jan-`yr`:15-dec-`yr` ($var)_month_ave_($trans), color;\
 plot/nolab/nokey/over/thick/color=($color($yy))/sym=26/t=15-jan-`yr`:15-dec-`yr` ($var)_month_ave_($trans);\
 let yr = `yr + 1`)

IF `ny GT 1` THEN define symbol yearlab = ($yearlab) - `yr-1`
annotate/norm/x=0.533/y=-0.205/siz=0.11/halign=-1 " ($yearlab)"

annotate/norm/x=0.5/y=-0.5/siz=0.14/halign=0 "($plot_title)"

say " "
say "($plot_annotation)"
say " "

if `($reference)` then

   use ($reference_data)
   if ($trans"0|max>1|*>0") then 
      plot/over($graydash) temp_hi[gt=tmonth]
      plot/over($whitedash) temp_hi[gt=tmonth]
   elif ($trans"0|min>1|*>0") then
      plot/over($graydash) temp_lo[gt=tmonth]
      plot/over($whitedash) temp_lo[gt=tmonth]
   elif ($trans"0|diff>1|*>0") then
      plot/over($graydash) temp_hi[gt=tmonth] - temp_lo[gt=tmonth]
      plot/over($whitedash) temp_hi[gt=tmonth] - temp_lo[gt=tmonth]
   endif
   cancel data ($reference_data)

elif ($var"|temperature>1|TEMPERATURE>1|*>0") then

   say To see Paine Field or Seatac average monthly min temp with temp plots,
   say add 3rd argument 1 for Paine Field or 2 for Seatac

endif

set data 1

say " "

! -------------- List monthly data --------------

let tm = t[gt=monthly]
let modate = TAX_DATESTRING(tm, tm, "month")



define symbol summary_file = summary_`thismonth`_`lasty`.txt

IF `($var"0|temperature>1|*>0") AND ($trans"0|max>1|*>0")` THEN
   say "Creating new monthly summary file ($summary_file)"
   say/clobber/outfile=($summary_file) "Summary for month of `thismonth` `lasty`"
   say/clobber/outfile=($summary_file) " "
ENDIF

say/append/outfile=($summary_file) " "
say/append/outfile=($summary_file) " "
say/append/outfile=($summary_file) "Monthly Average  ($var_title)  (($units))"
say/append/outfile=($summary_file) " "

if ($reference) then
   use data/everett_climatology.nc
   use data/seattle_climatology.nc

   if ($trans"0|max>1|*>0") THEN 
      define symbol hilo_var = temp_hi
      define symbol hilo_lab = High
   elif ($trans"0|min>1|*>0") then
      define symbol hilo_var = temp_lo
      define symbol hilo_lab = Low
   elif ($trans"0|diff>1|*>0") then
      define symbol hilo_var = temp_hi-temp_lo
      define symbol hilo_lab = Temp Diff
   endif

   use ($reference_data)

   set data 1

   let m1 = 1
   let yr = firsty
   cancel symbol comma
   cancel symbol varlist

   repeat/range=1:`ny`/name=yy (\
   let/title="`yr` monthly average" y`yr` = reshape(($var)_month_ave_($trans)[L=`m1`:`m1+11`], t[gt=month_irreg]);\
   define symbol varlist = ($varlist)($comma)y`yr`;\
   define symbol comma = , ;\
   let m1 = `m1+12`;\
   let yr = `yr+1`)

   if ($trans"0|diff>1|*>0") then
      let/like=($var) Paine = temp_hi[d=2]-temp_lo[d=2]
      let/like=($var) Seatac = temp_hi[d=3]-temp_lo[d=3]
   else
      let/like=($hilo_var)[d=2] Paine = ($hilo_var)[d=2]
      let/like=($hilo_var)[d=3] Seatac = ($hilo_var)[d=3]
   endif
   list ($varlist), Paine, Seatac

   list/append/file=($summary_file)  ($varlist), Paine, Seatac

else

   let m1 = 1
   let yr = firsty
   cancel symbol comma
   cancel symbol varlist

   repeat/range=1:`ny`/name=yy (\
   let/title="`yr` monthly average" y`yr` = reshape(($var)_month_ave_($trans)[L=`m1`:`m1+11`], t[gt=month_irreg]);\
   define symbol varlist = ($varlist)($comma)y`yr`;\
   define symbol comma = ,;\
   let m1 = `m1+12`;\
   let yr = `yr+1`)
   list ($varlist)

   list/append/file=($summary_file)  ($varlist)
endif

say 
say "See file ($summary_file)"
